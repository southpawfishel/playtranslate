import java.util.Properties

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.serialization)
}

val localProps = Properties()
val localPropsFile = rootProject.file("local.properties")
if (localPropsFile.exists()) localPropsFile.inputStream().use { localProps.load(it) }

android {
    namespace = "com.playtranslate"
    compileSdk = 36

    defaultConfig {
        // Fork identity: distinct from upstream's com.playtranslate so this
        // build installs ALONGSIDE the released app rather than colliding with
        // it (they are signed by different keys, so an in-place update is
        // impossible anyway). `namespace` above deliberately stays
        // com.playtranslate — that is the code package / R class, not the
        // installed identity. Anything deriving an authority or a path from
        // the app id must use ${applicationId} / context.packageName.
        applicationId = "com.davesies.translate"
        minSdk = 29
        targetSdk = 36
        versionCode = 16
        versionName = "3.0.1"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        // arm64-v8a + armeabi-v7a. arm64 is the real target — every
        // post-2019 Play-Store-eligible device is arm64. armeabi-v7a is the
        // compatibility tier (ML Kit translation works on 32-bit devices),
        // but :mnn intentionally ships arm64 only, so on a 32-bit slice
        // there are NO MNN libs at all and the Settings UI hides the MNN
        // backend rows via OnDeviceLlmBackend.supportsRequiredAbi(). x86_64
        // dropped — re-add for emulator testing if needed.
        ndk {
            abiFilters += listOf("arm64-v8a", "armeabi-v7a")
        }

        buildConfigField("String", "DEEPL_API_KEY",
            "\"${localProps.getProperty("deepl.api.key", "")}\"")

    }

    signingConfigs {
        getByName("debug") {
            // Uses default debug keystore at ~/.android/debug.keystore
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
            signingConfig = signingConfigs.getByName("debug")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlin {
        jvmToolchain(17)
        compilerOptions {
            freeCompilerArgs.add("-Xannotation-default-target=param-property")
        }
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    sourceSets {
        getByName("androidTest") {
            // OCR grouping corpus — a separate private repo checked out at
            // ocr-grouping/ (gitignored; the docs/ pattern). Mounting its
            // assets/ bundles the seeds into the test APK; an absent checkout
            // just yields an empty corpus (OcrGroupingHarnessTest skips loudly).
            assets.srcDir(rootProject.file("ocr-grouping/assets"))
        }
    }

    androidResources {
        // Generate res/xml/localeConfig from the values-* folders and wire
        // android:localeConfig into the merged manifest, so the app appears
        // under Settings -> System -> Languages -> App Languages on Android 13+.
        // The unqualified values/ resources are English; declared in
        // res/resources.properties (unqualifiedResLocale).
        generateLocaleConfig = true
    }

    packaging {
        // MNN ships standard .so libs that work fine with modern (mmap-loaded)
        // packaging. The legacy-packaging flag that lived here for `:llama`'s
        // GGML_BACKEND_DL=ON dlopen pattern is no longer needed after the
        // :llama strip.
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
            excludes += "/META-INF/LICENSE.md"
            excludes += "/META-INF/NOTICE.md"
            excludes += "/META-INF/CONTRIBUTORS.md"
            excludes += "/META-INF/LICENSE"
            excludes += "/META-INF/NOTICE"
            // KOMORAN model. Now shipped inside the KO source pack (see
            // scripts/build_latin_dict.py --komoran-jar); KoreanEngine
            // constructs Komoran(String modelPath) pointed at the
            // installed pack dir. Strip both LIGHT (~1.75 MB, what we
            // ship in the pack) and FULL (~4.2 MB, unused) models.
            excludes += "models_light/**"
            excludes += "models_full/**"
            // HanLP portable data/. Now shipped inside the ZH source
            // pack (see scripts/build_zh_dict.py --hanlp-jar);
            // ChineseEngine installs a PackAwareHanlpAdapter that
            // redirects HanLP's file reads to the pack's tokenizer/
            // dir. Strips ~22 MB of HanLP classpath resources.
            //
            // CAUTION: opencc4j ALSO ships dictionaries under data/dictionary/
            // (~1.1 MB of OpenCC STPhrases/TWVariants/HKVariants/… flat *.txt
            // files it loads via getResourceAsStream at runtime). A blanket
            // `data/dictionary/**` exclude strips those too and crashes the
            // Traditional-Chinese converter (ExceptionInInitializerError → null
            // resource stream in STPhraseData.<clinit>). So strip HanLP's entries
            // by name and let opencc4j's OpenCC tables survive. Both deps are
            // version-pinned, so this file list is stable; revisit on upgrade.
            excludes += "data/dictionary/CoreNatureDictionary*"
            excludes += "data/dictionary/stopwords.txt.bin"
            excludes += "data/dictionary/custom/**"
            excludes += "data/dictionary/organization/**"
            excludes += "data/dictionary/other/**"
            excludes += "data/dictionary/person/**"
            excludes += "data/dictionary/pinyin/**"
            excludes += "data/dictionary/place/**"
            excludes += "data/dictionary/synonym/**"
            excludes += "data/dictionary/tc/**"
            excludes += "data/model/**"
        }
    }

    testOptions {
        unitTests.isIncludeAndroidResources = true
        unitTests.isReturnDefaultValues = true
    }
}

// Provisions the JDK via Gradle's toolchain API so the build doesn't depend on
// whichever JDK the user has on PATH. Combined with the foojay-resolver plugin
// in settings.gradle.kts and `auto-download=true` in gradle.properties, Gradle
// fetches and caches a JDK 17 under ~/.gradle/jdks if one is not installed.
kotlin {
    jvmToolchain(17)
}

dependencies {
    implementation(project(":mnn"))
    implementation(project(":bergamot"))

    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.appcompat)
    implementation(libs.material)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.androidx.recyclerview)
    implementation(libs.androidx.lifecycle.viewmodel.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.activity.ktx)
    implementation(libs.androidx.fragment.ktx)
    implementation(libs.kotlinx.coroutines.android)

    // ML Kit
    implementation(libs.mlkit.text.recognition.japanese)
    implementation(libs.mlkit.text.recognition)          // Latin base SDK (Phase 3)
    implementation(libs.mlkit.text.recognition.chinese)   // Chinese OCR (Phase 4)
    implementation(libs.mlkit.text.recognition.korean)    // Korean OCR
    implementation(libs.mlkit.text.recognition.devanagari) // Devanagari/Hindi OCR
    implementation(libs.mlkit.translate)
    implementation(libs.mlkit.language.id)

    // Networking
    implementation(libs.okhttp)
    implementation(libs.gson)                       // streaming Yomitan bank parsers only
    implementation(libs.kotlinx.serialization.json) // reflective DTO (de)serialization

    // Japanese morphological analysis: Sudachi/UniDic (see docs/sudachi-spike-report.md);
    // replaced the abandoned kuromoji-ipadic (2007 IPADIC).
    implementation(libs.sudachi)

    // Lucene Snowball stemmer (Phase 3: Latin/English stemming)
    implementation(libs.lucene.analyzers.common)

    // KOMORAN (Korean morphological analyzer — TRIE + statistical OOV).
    // Used instead of Lucene Nori because Nori's AttributeFactory touches
    // java.lang.ClassValue, which Android ART does not ship. KOMORAN is
    // pure Java and Android-compatible.
    implementation(libs.komoran)

    // HanLP CRF segmenter (Phase 4: Chinese word segmentation)
    implementation(libs.hanlp)

    // OpenCC (opencc4j): render-time Simplified->Traditional Chinese conversion
    // for Traditional target output (s2t / s2tw phrase-level / s2hk glyph-level).
    // Pure-JVM, Apache-2.0; no NDK, so it runs on every ABI.
    implementation(libs.opencc4j)

    // Unit tests
    testImplementation(libs.junit)
    testImplementation(libs.robolectric)
    testImplementation(libs.androidx.test.core)

    // On-device instrumented tests (OCR golden-set evaluation)
    androidTestImplementation(libs.androidx.test.ext.junit)
    androidTestImplementation(libs.androidx.test.runner)
    androidTestImplementation(libs.androidx.test.rules)
    androidTestImplementation(libs.kotlinx.coroutines.test)

    // PaddleOCR: OpenCV for DBNet postprocessing + crop rectification.
    // Promoted from androidTest to a main dependency for the debug-only
    // in-app PaddleOCR toggle (DebugUsePaddleOcr) — ships OpenCV native .so
    // into the app APK. Experimental / debug-gated; not a shipped feature.
    // See docs/paddleocr-spike-report.md (verdict: NO-GO for production).
    implementation(libs.opencv)

    // CameraX for the camera tool (Settings → Tools → Camera).
    implementation(libs.camerax.core)
    implementation(libs.camerax.camera2)
    implementation(libs.camerax.lifecycle)
    implementation(libs.camerax.view)
}
